C*******************************************************************
C*******************                             *******************
C*******************     TEAM-S  -----  K-E      *******************
C*******************                             *******************
C*******************************************************************
C*********                                                **********
C*********  TURBULENT ELLIPTIC AXISYMMETRIC MANCHESTER    **********
C*********                                                **********
C*********           BY: P.G.HUANG                        **********
C*********                  B.E.LAUNDER                   **********
C*********                       M.A.LESCHZINER           **********
C*********                                                **********
C*********                   1983                         **********
C*********                                                **********
C*********                                                **********
C*********           DIVISION OF THERMOFLUIDS             **********
C*********     DEPARTMENT OF MECHANICAL ENGINEERING       **********
C*********                   UMIST                        **********
C*********               MANCHESTER M60                   **********
C*********                                                **********
C*******************************************************************
C*******************************************************************
      INCLUDE 'UCOMMON.INC'

	CALL SET
	CALL GRID
	CALL INLET
	CALL OUTPI
C********* START ITERATION
10	NITER = NITER + 1
	CALL PROPS
	CALL CALCU
	CALL CALCV
	CALL CALCP
	CALL CALCTE
	CALL CALCED

	RESORM = RESORM/FLOWIN
	RESORU = RESORU/XMONIN
	RESPRV = RESPRV/XMONIN
	RESORK = RESORK/XMONIN
	RESORE = RESORE/XMONIN
	WRITE(6,311)NITER,RESORU,RESORV,RESORM,RESORK,RESORE,U(IMON,JMON)
     +           ,V(IMON,JMON),P(IMON,JMON),TE(IMON,JMON),ED(IMON,JMON)
      SORCE = AMAX1(RESORU,RESORV,RESORM,RESORK,RESORE)
	IF(SORCE.GE.1.E+30.AND.NITER.GT.30)STOP
	IF(SORCE.LE.SORMAX)GO TO 20
	IF(NITER.LT.MAXIT)GO TO 10
20	CONTINUE
C********* END ITERATION
	CALL OUTP
311	FORMAT(1X,I4,1P10E10.3)
	STOP
	END
C*************************     SUB. USER        ********************
	SUBROUTINE USER
      INCLUDE 'UCOMMON.INC'
	LOGICAL LREAD,LWRITE
	DIMENSION YF(5)

	DATA YF/0.163,0.321,0.484,0.647,0.811/
C********* SET DWFUALT VALUES
	ENTRY SET

	URFU=0.5
	URFV=0.5
	URFP=0.8
	URFK=0.8
	URFGEN=1.0
	URFE=0.8
	URFVIS=0.8
	URFDEN=1.0

	NSWPU=1
	NSWPV=1
	NSWPP=2
	NSWPK=1
	NSWPE=1

	LREAD=.FALSE.
	LWRITE=.TRUE.
	NQUV=0
	NQEK=0
	NITER=0
	MAXIT=3
	IPREF=2
	JPREF=2
	IMON=3
	JMON=3
	SORMAX=5.E-3
	VISCOS=1.42E-5
	DENSIT=1.
	UIN=3.5
	TKEIN=0.015*(3.5*3.5)
	TEDIN=2.876
	CMU=0.09
	C1=1.44
	C2=1.92
	CAPPA=.4187
	ELOG=9.793
	PRED=CAPPA**2/(C2-C1)/SQRT(CMU)
	PRTE=1.
	CMU25=CMU**0.25
	CMU75=CMU**0.75
	RETURN
C********* GRID SPECIFICATION & CALCULATE GEOMETRICAL QUANTITIES
      ENTRY GRID
      R(1)=1.D0
      IND=1
      NIM1=M-1
      NJM1=N-1
      XL=1.0D0
      YL=1.0D0
      HSTEP=0.0D0
      RACLY=1.1D0
      RAY=1.0/RACLY
      RACLX=1.1D0
      RAX=1.0/RACLX
      JSTEP=1
      JSTP1=JSTEP+1
      JSTM1=JSTEP-1
      ISTEP=1
      ISTP1=ISTEP+1
      ISTM1=ISTEP-1
      XU(1)=0.0D0
      YV(1)=0.0D0
      XU(NIM1)=XL
      YV(NJM1)=YL
      TOY=0.0D0
      JCL=(NJM1-JSTEP)/2+JSTEP
      JCLP1=JCL+1
      ICL=(NIM1-ISTEP)/2+ISTEP
      ICLP1=ICL+1
      XU(1)=0.0D0
      YV(1)=0.0D0
      TOY=0.0D0
      DO 82 J=JSTP1,JCL
   82 TOY=TOY+RACLY**(J-JSTP1)
      DY=(0.5*YL)/TOY
      DO 124 J=JSTP1,JCL
  124 YV(J)=YV(J-1)+DY*RACLY**(J-JSTP1)
      TOY=0.0D0
      DO 105 J=JCLP1,NJM1
  105 TOY=TOY+RAY**(J-JCLP1)
      DY=(YL-0.5*YL)/TOY
      DO 106 J=JCLP1,NJM1
  106 YV(J)=YV(J-1)+DY*RAY**(J-JCLP1)
      TOX=0.0D0
      DO 107 I=ISTP1,ICL
  107 TOX=TOX+RACLX**(I-ISTP1)
      DX=(0.5*XL)/TOX
      DO 108 I=ISTP1,ICL
  108 XU(I)=XU(I-1)+DX*RACLX**(I-ISTP1)
      TOX=0.0D0
      DO 109 I=ICLP1,NIM1
  109 TOX=TOX+RAX**(I-ICLP1)
      DX=(XL-0.5*XL)/TOX
      DO 110 I=ICLP1,NIM1
  110 XU(I)=XU(I-1)+DX*RAX**(I-ICLP1)
      CALL INIT
      RETURN
C********* INLET CONDITION AND INITIAL GUESS
	ENTRY INLET
	DO 213 I=1,NI
	U(I,1)=0.
	V(I,1)=0.
	V(I,NJ)=0.
213	U(I,NJ)=0.
	DELTA=0.50*HSTEP
	TLS=0.09*DELTA
	TKEIN=1.5E-04*UIN*UIN
	TKEIN1=0.8E-02*UIN*UIN
	A=(TKEIN-TKEIN1)/DELTA
	DO 144 J=JSTP1,NJM1
	YY=Y(J)-HSTEP
	IF(YY.GT.HSTEP) YY=2.0*HSTEP-YY
	U(1,J)=UIN*(YY/DELTA)**(1.0/5.6)
	IF(YY.LE.HSTEP.AND.YY.GE.DELTA) U(1,J)=UIN
	TE(1,J)=A*YY+TKEIN1
	IF(YY.LE.HSTEP.AND.YY.GE.DELTA) TE(1,J)=TKEIN
	ED(1,J)=TE(1,J)**(1.5)/TLS
	TLS1=CAPPA*YY
	IF(YY.LT.DELTA) ED(1,J)=TE(1,J)**(1.5)/TLS1
144	CONTINUE
	DO 202 J=2,NJM1
	DO 202 I=2,NI
	U(I,J)=UIN
	TE(I,J)=TKEIN
	ED(I,J)=TEDIN
202	CONTINUE
	DO 503 I=2,NIM1
	YPLUSS(I)=11.0
503	YPLUSN(I)=11.0
	DO 204 J=1,JSTEP
204	XPLUSW(J)=11.
C-------- CALCULATE INLET MASS FLOWRATE
	FLOWIN=0.0
	XMONIN=0.0
	DO 657 J=2,NJM1
	ARDEN=DEN(1,J)*SYCV(J)
	FLOWIN= FLOWIN+ARDEN*U(1,J)
	XMONIN= XMONIN+ARDEN*U(1,J)**2
657	CONTINUE
      OPEN(11,FILE='OUTPUT.plt')
	REWIND 11
	IF(LREAD)READ(11,*) ((U(K,L),V(K,L),P(K,L),TE(K,L),ED(K,L),
     +K=1,NI),L=1,NJ),(YPLUSN(I),I=2,NIM1),NITER
	DO 350 I=1,NI
	DO 350 J=1,NJ
350	VIS(I,J)=CMU*DEN(I,J)*TE(I,J)**2/(ED(I,J)+TINY)+VISCOS
	RETURN
C********* OUTPUT
	ENTRY OUTPI
	WRITE(6,383)
	WRITE(6,401)
	WRITE(6,299)NQUV,NQKE
	WRITE(6,402)HSTEP,XL,YL,UIN,TKEIN,TEDIN,DENSIT,VISCOS
	WRITE(6,313)IMON,JMON
	WRITE(6,403)
383	FORMAT("1")
401	FORMAT(5X,32H TURBULENT BACKWARD FACING STEP )
299	FORMAT(/////,5X,"NQUV=",I2,13X,"NQKE=",I2)
402	FORMAT(/////,5X,"HSTEP=",1PE10.3,5X,"  XL=",E10.3,5X,"  YL="
     +,E10.3,/,5X,"  UIN=",E10.3,5X,"TKEIN=",E10.3,5X,"TEDIN=",E10.3,/,
     +5X,"DEN=",E10.3,5X,"  VIS=",E10.3)
403	FORMAT(1X,4HITER,10H  RESORU ,10H  RESORV ,10H  RESORM ,
     +10H  RESORK ,10H  RESORE ,10H    UU    ,10H   VV    ,
     +10H    P    ,10H    TE   ,10H    ED    )
313	FORMAT(////////,"       IMON=",I3,"   JMON=",I3)
	RETURN
C********* UPDATE PROPERTIES
	ENTRY PROPS
	DO 900 I=1,NI
	DO 900 J=1,NJ
	VISNEW=DEN(I,J)*TE(I,J)**2*CMU/ABS(ED(I,J)+TINY)+VISCOS
	VIS(I,J)=VIS(I,J)+URFVIS*(VISNEW-VIS(I,J))
900	CONTINUE
	DO 901 J=1,NJ
901	VIS(NI,J)=VIS(NIM1,J)
	RETURN
C********* U-VELOCITY B C
	ENTRY MODU
C
C--------TOP WALL
	J=NJM1
	DO 210 I=2,NIM2
	YP=VCVJ(NJM1)
	SQRTK=SQRT(ABS(TE(I,J)+FX(I)*(TE(I+1,J)-TE(I,J))))
	YPLUSA=YPLUSN(I)+FX(I)*(YPLUSN(I+1)-YPLUSN(I))
	DENU= DEN(I,J)+FX(I)*(DEN(I+1,J)-DEN(I,J))
	IF(YPLUSA.LE.11.63) GO TO 211
	TMULT=DENU*CMU25*SQRTK*CAPPA/ALOG(ELOG*YPLUSA)
	GO TO 212
211	TMULT=VISOCS/YP
212	TAUN(I)=-TMULT*U(I,J)
	SP(I,J)=SP(I,J)-TMULT*UXCV(I)
210	AN(I,J)=0.
	TAUN(1)=TAUN(2)
	TAUN(NIM1)=TAUN(NIM2)
	TAUN(NI)=TAUN(NIM1)
C--------BOTTOM WALL
	J=2
	DO 219 I=2,NIM2
	YP=VCVJP(1)
	SQRTK=SQRT(ABS(TE(I,J)+FX(I)*(TE(I+1,J)-TE(I,J))))
	YPLUSA=YPLUSS(I)+FX(I)*(YPLUSS(I+1)-YPLUSS(I))
	DENU= DEN(I,J)+FX(I)*(DEN(I+1,J)-DEN(I,J))
	IF(YPLUSA.LE.11.63) GO TO 214
	TMULT=DENU*CMU25*SQRTK*CAPPA/ALOG(ELOG*YPLUSA)
	GO TO 215
214	TMULT=VISOCS/YP
215	TAUS(I)=-TMULT*U(I,J)
	SP(I,J)=SP(I,J)-TMULT*UXCV(I)
219	AS(I,J)=0.0
	TAUS(1)=TAUS(2)
	TAUS(NIM1)=TAUS(NIM2)
	TAUS(NI)=TAUS(NIM1)
C--------SIDE WALL
	DO 263 J=1,JSTEP
263	AW(2,J)=0.0
C--------OUTLET
	GO TO 269
	ARDENT=0.
	FLOW=0.
	DO 264 J=2,NJM1
	DENU= DEN(NIM2,J)+FX(NIM2)*(DEN(NIM1,J)-DEN(NIM2,J))
	ARDEN= DENU*SYCV(J)
	ARDENT=ARDENT +ARDEN
264	FLOW = FLOW + ARDEN*U(NIM2,J)
	UINC=(FLOWIN-FLOW)/ARDENT
	DO 205 J=2,NJM1
	AE(NIM2,J)=0.0
	U(NIM1,J)= U(NIM2,J)+ UINC
	TE(NI,J) = TE(NIM1,J)
	ED(NI,J) = ED(NIM1,J)
205	U(NI,J)= U(NIM1,J)
269	RETURN
C
C
C********* V-VELOCITY B C
	ENTRY MODV
C--------TOP WALL
	DO 319 I=2,NIM1
319	AN(I,NJM2)=0.0
C--------SIDE WALL
	I=2
	DO 310 J=2,JSTM1
	XP=UCVIP(1)
	SQRTK=SQRT(ABS(TE(I,J)+FY(J)*(TE(I,J+1)-TE(I,J))))
	XPLUSA=XPLUSW(J)+FY(J)*(XPLUSW(J+1)-XPLUSW(J))
	DENV=DEN(I,J)+FY(J)*(DEN(I,J+1)-DEN(I,J))
	IF (XPLUSA.LE.11.63) GO TO 311
	TMULT = DENV*CMU25*SQRTK*CAPPA/ALOG(ELOG*XPLUSA)
	GO TO 312
311	TMULT=VISCOS/XP
312	TAUW(J)=-TMULT*V(I,J)
	SP(I,J)=SP(I,J)-TMULT*VYCV(J)
310	AW(I,J)=0.
	TAUW(JSTEP)=TAUW(JSTM1)
C--------BOTTOM WALL
	DO 304 I=2,NIM1
304	AS(I,2)=0.
	RETURN
C
C
C********* PRESSURE B C
	ENTRY MODP
	RETURN

C********* TURB. K. B C
	ENTRY MODTE
	DO 44 J=2,NJM1
44	AE(NIM1,J)=0.0
C--------TOP WALL
	J=NJM1
	DO 610 I=2,NIM1
	TVIS=VIS(I,J)-VISCOS+TINY
	VOL= RSYCV(J)*SXCV(I)
	SU(I,J) = SU(I,J)-GEN(I,J)*VOL
	SP(I,J) = SP(I,J)+CMU*DEN(I,J)**2*ABS(TE(I,J))/TVIS*VOL
	YP=0.5*SYCV(J)
	SQRTK=SQRT(ABS(TE(I,J)))
	GENCOU=0.5*(ABS(TAUN(I-1)*U(I-1,J))+ABS(TAUN(I)*U(I,J)))/YP
	YPLUSN(I)=DEN(I,J)*SQRTK*CMU25*YP/VISCOS
	UP=0.5*(U(I,J)+U(I-1,J))
	UN=0.5*(U(I,J+1)+U(I-1,J+1))
	US=0.5*(U(I,J-1)+U(I-1,J-1))
	US=US+FY(J-1)*(UP-US)
	VE=0.5*(V(I,J)+V(I,J-1)+FX(I)*(V(I+1,J)+V(I+1,J-1)-V(I,J)-V(I,J-1)
     +))
	VW=0.5*(V(I-1,J)+V(I-1,J-1)+FX(I-1)*(V(I,J)+V(I,J-1)-V(I-1,J)
     +  -V(I-1,J-1)))
	DVDX=(VE-VW)/SXCV(I)
	UN=0.5*(U(I,J)+U(I-1,J)+FY(J)*(U(I,J+1)+U(I-1,J+1)-U(I,J)-U(I-1,J)
     +))
	US=0.5*(U(I,J-1)+U(I-1,J-1)+FY(J-1)*(U(I,J)+U(I-1,J)-U(I,J-1)
     +  -U(I-1,J-1)))
	DUDY=(UN-US)/SYCV(J)
	GENRES=GEN(I,J)-URFGEN*TVIS*(DVDX+DUDY)**2
	GEN(I,J)=URFGEN*GENCOU+GENRES
	IF (YPLUSN(I).LE.11.63) GO TO 611
	DITERM=DEN(I,J)*CMU75*SQRTK*ALOG(ELOG*YPLUSN(I))/(CAPPA*YP)
	GO TO 612
611	DITERM=DEN(I,J)*CMU75*SQRTK*YPLUSN(I)/YP
612	SU(I,J)=GEN(I,J)*VOL+SU(I,J)
	SP(I,J)=-DITERM*VOL+SP(I,J)
610	AN(I,J)=0.
C--------BOTTOM WALL
	J=2
	DO 613 I=2,NIM1
	TVIS=VIS(I,J)-VISCOS+TINY
	VOL= RSYCV(J)*SXCV(I)
	SU(I,J) = SU(I,J)-GEN(I,J)*VOL
	SP(I,J) = SP(I,J)+CMU*DEN(I,J)**2*ABS(TE(I,J))/TVIS*VOL
	YP=0.5*SYCV(J)
	SQRTK=SQRT(ABS(TE(I,J)))
	GENCOU=0.5*(ABS(TAUS(I-1)*U(I-1,J))+ABS(TAUS(I)*U(I,J)))/YP
	YPLUSS(I)=DEN(I,J)*SQRTK*CMU25*YP/VISCOS
	UP=0.5*(U(I,J)+U(I-1,J))
	UN=0.5*(U(I,J+1)+U(I-1,J+1))
	US=0.5*(U(I,J-1)+U(I-1,J-1))
	UN=UP+FY(J)*(UN-UP)
	VE=0.5*(V(I,J)+V(I,J-1)+FX(I)*(V(I+1,J)+V(I+1,J-1)-V(I,J)-V(I,J-1)
     +))
	VW=0.5*(V(I-1,J)+V(I-1,J-1)+FX(I-1)*(V(I,J)+V(I,J-1)-V(I-1,J)
     +  -V(I-1,J-1)))
	DVDX=(VE-VW)/SXCV(I)
	UN=0.5*(U(I,J)+U(I-1,J)+FY(J)*(U(I,J+1)+U(I-1,J+1)-U(I,J)-U(I-1,J)
     +))
	US=0.5*(U(I,J-1)+U(I-1,J-1)+FY(J-1)*(U(I,J)+U(I-1,J)-U(I,J-1)
     +  -U(I-1,J-1)))
	DUDY=(UN-US)/SYCV(J)
	GENRES=GEN(I,J)-URFGEN*TVIS*(DVDX+DUDY)**2
	GEN(I,J)=URFGEN*GENCOU+GENRES
	IF (YPLUSS(I).LE.11.63) GO TO 615
	DITERM=DEN(I,J)*CMU75*SQRTK*ALOG(ELOG*YPLUSS(I))/(CAPPA*YP)
	GO TO 614
615	DITERM=DEN(I,J)*CMU75*SQRTK*YPLUSS(I)/YP
614	SU(I,J)=GEN(I,J)*VOL+SU(I,J)
	SP(I,J)=-DITERM*VOL+SP(I,J)
613	AS(I,J)=0.
C--------WEST WALL
	I=2
	DO 620 J=2,JSTEP
	TVIS = VIS(I,J)-VISCOS+TINY
	VOL=RSYCV(J)*SXCV(I)
	SU(I,J)=SU(I,J)-GEN(I,J)*VOL
	SP(I,J)=SP(I,J)+CMU*DEN(I,J)**2*ABS(TE(I,J))/TVIS*VOL
	XP=0.5*SXCV(I)
	SQRTK=SQRT(ABS(TE(I,J)))
	XPLUSW(J)=DEN(I,J)*SQRTK*CMU25*XP/VISCOS
	GENCOU=0.5*(ABS(TAUW(J-1)*V(I,J-1))+ABS(TAUW(J)*V(I,J)))/XP
	VE=0.5*(V(I,J)+V(I,J-1)+FX(I)*(V(I+1,J)+V(I+1,J-1)-V(I,J)-V(I,J-1)
     +))
	VW=0.5*(V(I-1,J)+V(I-1,J-1)+FX(I-1)*(V(I,J)+V(I,J-1)-V(I-1,J)
     +  -V(I-1,J-1)))
	DVDX=(VE-VW)/SXCV(I)
	UN=0.5*(U(I,J)+U(I-1,J)+FY(J)*(U(I,J+1)+U(I-1,J+1)-U(I,J)-U(I-1,J)
     +))
	US=0.5*(U(I,J-1)+U(I-1,J-1)+FY(J-1)*(U(I,J)+U(I-1,J)-U(I,J-1)
     +  -U(I-1,J-1)))
	DUDY=(UN-US)/SYCV(J)
	GENRES=GEN(I,J)-URFGEN*TVIS*(DVDX+DUDY)**2
	GEN(I,J)=URFGEN*GENCOU+GENRES
	IF (XPLUSW(J).LE.11.63) GO TO 621
	DITERM=DEN(I,J)*CMU75*SQRTK*ALOG(ELOG*XPLUSW(J))/(CAPPA*XP)
	GO TO 622
621	DITERM=DEN(I,J)*CMU75*SQRTK*XPLUSW(J)/XP
622	SU(I,J)=SU(I,J)+GEN(I,J)*VOL
	SP(I,J)=SP(I,J)-DITERM*VOL
620	AW(I,J)=0.
	RETURN
C
C
C********* TURB. e. B C
	ENTRY MODED
	DO 23 J=2,NJM1
23	AE(NIM1,J)=0.0
C--------TOP WALL
	J=NJM1
	DO 710 I=2,NIM1
	YP=0.5*SYCV(J)
	TERM=CMU75/(CAPPA*YP)
	SU(I,J)=GREAT*TERM*ABS(TE(I,J))**1.5
710	SP(I,J)=-GREAT
C--------BOTTOM WALL
	J=2
	DO 730 I=2,NIM1
	YP=0.5*SYCV(J)
	TERM=CMU75/(CAPPA*YP)
	SU(I,J)=GREAT*TERM*ABS(TE(I,J))**1.5
	SP(I,J)=-GREAT
730	CONTINUE
C--------SIDE WALL
	I=2
	DO 720 J=2,JSTEP
	XP=0.5*SXCV(I)
	TERM=CMU75/(CAPPA*XP)
	SU(I,J)=GREAT*TERM*ABS(TE(I,J))**1.5
720	SP(I,J)=-GREAT
	RETURN
C********* OUTPUT
	ENTRY OUTP
	REWIND 1
	REWIND 11
      WRITE(11,*) 'TITLE="STEP" '
      WRITE(11,*) 'VARIABLES=X,Y,U,V,P,TE,ED,DEN,VIS'
      WRITE(11,*) 'ZONE t="STEP" '
      WRITE(11,1008) NI,NJ
 1008 FORMAT(1X,'I=',I4,'J=',I4,'F=POINT')
      DO 321 J=1,NJ
      DO 321 I=1,NI
      IF(ABS(V(I,J)).LT.1.E-33)V(I,J)=0.0
      V1=V(I,J)
      IF(J.EQ.1)GOTO 118
      V1=(V(I,J)+V(I,J-1))/2
  118 IF(ABS(U(I,J)).LT.1.E-33)U(I,J)=0.0
      U1=U(I,J)
      IF(I.EQ.1)GOTO 119
      U1=(U(I,J)+U(I-1,J))/2
  119 VEC=SQRT(U1**2+V1**2)
      WRITE(11,1000) X(I),Y(J),U1,V1,P(I,J),TE(I,J),ED(I,J)
     +,DEN(I,J),VIS(I,J)
 1000 FORMAT(1X,23(D17.8,2X)) 
321	CONTINUE
      END FILE 11

	DO 116 IF=1,5
	YY=YF(IF)
	WRITE(1,117)YF(IF)
117	FORMAT("     STATION : = ",F7.4," M ")
	YY=YF(IF)*XL
	DO 112 I=1,NIM1
	IF(XU(I).LT.YY)GO TO 112
	IGRT=I
	ISML=I-1
	GOTO 113
112	CONTINUE
113	FF=XU(IGRT)-XU(ISML)
	FSML=(XU(IGRT)-YY)/FF
	FGRT=(YY-XU(ISML))/FF
	DO 114 J=1,NJ
	VV=(U(ISML,J)*FSML+U(IGRT,J)*FGRT)
	YJ=Y(J)
114	WRITE(1,115)J,YJ,VV
115	FORMAT(I3,1P2E11.4)
116	CONTINUE
	WRITE(6,121)(J,XPLUSW(J),TAUW(J),J=1,NJ)
121	FORMAT(1X,I3,1P2E10.3)
	CALL PRINT(1,1,NIM1,NJ,XU,Y,U,HEDU)
	CALL PRINT(1,1,NI,NJM1,X,YV,V,HEDV)
	CALL PRINT(1,1,NI,NJ,X,Y,P,HEDP)
	CALL PRINT(1,1,NI,NJ,X,Y,TE,HEDK)
	CALL PRINT(1,1,NI,NJ,X,Y,ED,HEDE)
	CALL PRINT(1,1,NI,NJ,X,Y,VIS,HEDVIS)
	RETURN
	END

C*************************     INITIALIZE       ********************
	SUBROUTINE INIT

      INCLUDE 'UCOMMON.INC'
C********* DEFAULT VALUES
	GREAT=1.E+30
	TINY=1.E-30
	DO 410 I=1,6
	HEDU(I)=4H U U
	HEDV(I)=4H V V
	HEDP(I)=4H P P
	HEDK(I)=4H K K
	HEDE(I)=4H E E
410	HEDVIS(I)=4HVISC 
C********* CALCULATE GEOMETRICAL QUANTITIES
	NI=NIM1+1
	NJ=NJM1+1
	NIM2=NIM1-1
	NJM2=NJM1-1
	X(1)=XU(1)
	X(NI)=XU(NIM1)
	Y(1)=YV(1)
	Y(NJ)=YV(NJM1)
	DO 100 I=2,NIM1
100	X(I)=0.5*(XU(I-1)+XU(I))
	DO 150 J=2,NJM1
150	Y(J)=0.5*(YV(J-1)+YV(J))
	SXCV(1)=TINY
	SXCV(NI)=TINY
	SYCV(1)=TINY
	SYCV(NJ)=TINY
	DO 200 I=2,NIM1
200	SXCV(I)=XU(I)-XU(I-1)
	DO 250 J=2,NJM1
250	SYCV(J)=YV(J)-YV(J-1)
	DO 300 I=1,NIM1
300	UXCV(I)=X(I+1)-X(I)
	DO 350 J=1,NJM1
350	VYCV(J)=Y(J+1)-Y(J)
	UCVI(1)=0.0
	UCVIP(NIM1)=0.0
	VCVJ(1)=0.0
	VCVJP(NJM1)=0.0
	DO 400 I=2,NIM1
	UCVI(I)=0.5*SXCV(I)
400	UCVIP(I-1)=UCVI(I)
	DO 450 J=2,NJM1
	VCVJ(J)=0.5*SYCV(J)
450	VCVJP(J-1)=VCVJ(J)
	DO 500 I=1,NIM1
500	FX(I)=UCVI(I)/UXCV(I)
	DO 550 J=1,NJM1
550	FY(J)=VCVJ(J)/VYCV(J)
	IF(IND.EQ.2)GO TO 600
	DO 650 J=1,NJ
	R(J)=1.
650	RV(J)=1.
	GO TO 700
600	DO 750 J=2,NJ
750	R(J)=R(J-1)+VYCV(J-1)
	RV(1)=R(1)
	DO 800 J=2,NJM1
800	RV(J)=RV(J-1)+SYCV(J)
700	DO 850 J=1,NJ
850	RSYCV(J)=R(J)*SYCV(J)
	DO 900 J=1,NJM1
900	RVYCV(J)=0.5*(R(J+1)+R(J))*VYCV(J)
	DO 925 I=1,NIM1
925	B3XS(I)=UCVI(I)*UCVIP(I)*UXCV(I)
	DO 926 I=1,NI
926	B3XU(I)=SXCV(I)**3*0.25
	DO 927 J=1,NJM1
927	B3YS(J)=VCVJ(J)*VCVJP(J)*VYCV(J)
	DO 928 J=1,NJ
928	B3YV(J)=SYCV(J)**3*0.25
C********* SET VARIABLES TO ZERO
	DO 950 I=1,NI
	DO 950 J=1,NJ
	U(I,J)=0.0
	V(I,J)=0.0
	P(I,J)=0.0
	PP(I,J)=0.0
	DEN(I,J)=DENSIT
	VIS(I,J)=VISCOS
	DU(I,J)=0.0
	DV(I,J)=0.0
	SU(I,J)=0.0
	SP(I,J)=0.0
	TE(I,J)=0.0
	ED(I,J)=0.0
	GEN(I,J)=0.0
	AE(I,J)=0.0
	AW(I,J)=0.0
	AN(I,J)=0.0
	AS(I,J)=0.0
	SMPW(J)=0.0
950	CONTINUE
	RETURN
	END

C*************************     U-MOMENTUM       ********************
	SUBROUTINE CALCU

      INCLUDE 'UCOMMON.INC'
      LOGICAL LX,LY 

      NQUICK=NQUV
C********** ASSEMBLY OF COEFFICIENTS
      DO 100 I=2,NIM1
      DO 100 J=2,NJ
C********** AREAS,VOLUME,DELX & DELY
      AREAW=RSYCV(J)
      AREAS=UXCV(I)*RV(J-1)
      DELX1=UCVIP(I-1)
      DELX2=UCVI(I)
      DELY1=VCVJ(J-1)
      DELY2=VCVJP(J-1)
      B3X=B3XU(I)
      B3Y=B3YS(J-1)
      LX=I.GT.2.AND.I.LT.NIM1
      LY=J.GT.2.AND.J.LT.NJ
      VOL=RSYCV(J)*UXCV(I)
C********** CALCULATE CONVECTION
      DENWE=DEN(I,J)+FX(I)*(DEN(I+1,J)-DEN(I,J))
      DENWW=DEN(I-1,J)+FX(I-1)*(DEN(I,J)-DEN(I-1,J))
      CW=0.5*(DENWE*U(I,J)+DENWW*U(I-1,J))*AREAW
      DENSE=DEN(I+1,J-1)+FY(J-1)*(DEN(I+1,J)-DEN(I+1,J-1))
      DENSW=DEN(I,J-1)+FY(J-1)*(DEN(I,J)-DEN(I,J-1))
      CS=0.5*(DENSE*V(I+1,J-1)+DENSW*V(I,J-1))*AREAS
C********** CALCULATE DIFFUSION
      DW=VIS(I,J)*AREAW/SXCV(I)
      GAMSE=VIS(I+1,J-1)+FY(J-1)*(VIS(I+1,J)-VIS(I+1,J-1))
      GAMSW=VIS(I,J-1)+FY(J-1)*(VIS(I,J)-VIS(I,J-1))
      GAMS=0.5*(GAMSE+GAMSW)
      DS=GAMS*AREAS/VYCV(J-1)
C********** SCHEMES
      NSUP=-1
      IF(CW.GT.0.0)NSUP=1
      UP=0.5*FLOAT(1+NSUP)
      UM=0.5*FLOAT(1-NSUP)
      NSVP=-1
      IF(CS.GT.0.0)NSVP=1
      VP=0.5*FLOAT(1+NSVP)
      VM=0.5*FLOAT(1-NSVP)
      IF(LX.AND.NQUICK.EQ.1)GO TO 111
      TEMPW=DW-ABS(CW)*0.1
      COEW=0.0D0
      TEMPW=TEMPW/(DW+TINY)
      IF(TEMPW.GT.TINY)COEW=DW*TEMPW**5
      IF(.NOT.LX.AND.NQUICK.EQ.1)COEW=DW-0.5*ABS(CW)
      SPPX=0.0
      SUPX=0.0
      AE(I,J)=0.0D0
      GO TO 300
  111 KXP=I-NSUP
      LXP=KXP-(1+NSUP)/2
      DELX3=UM*DELX2-UP*DELX1-SXCV(KXP)*NSUP
      B1X=DELX2*DELX3*(DELX2-DELX3)
      B2X=DELX1*DELX3*(DELX3+DELX1)
      BX=B1X-B2X+B3X
      TEMP=B3X/BX*CW
      COEW=DW+(UM*B1X+UP*B2X)/BX*CW
      CW=CW-TEMP
      TEMPP=TEMP*UP
      TEMPM=TEMP*UM
      SPPX=-TEMPP
      SUPX=TEMPP*U(LXP,J)
      AW(I-1,J)=AW(I-1,J)-TEMPP
      AE(I,J)=TEMPM
      SP(I-1,J)=SP(I-1,J)+TEMPM
      SU(I-1,J)=SU(I-1,J)-TEMPM*U(LXP,J)
  300 CONTINUE
      IF(LY.AND.NQUICK.EQ.1)GO TO 222
      TEMPS=DS-ABS(CS)*0.1
      COES=0.0
      TEMPS=TEMPS/(DS+TINY)
      IF(TEMPS.GT.TINY)COES=DS*TEMPS**5
      IF(.NOT.LY)COES=DS-AMAX1(FY(J-1)*CS,-(1.-FY(J-1))*CS)
      SPPY=0.0
      SUPY=0.0
      AN(I,J)=0.0
      GO TO 400
  222 KYP=J-1-NSVP
      LYP=KYP+(1-NSVP)/2
      DELY3=VM*DELY2-VP*DELY1-VYCV(KYP)*NSVP
      B1Y=DELY2*DELY3*(DELY2-DELY3)
      B2Y=DELY1*DELY3*(DELY1+DELY3)
      BY=B1Y-B2Y+B3Y
      TEMP=B3Y/BY*CS
      COES=DS+(VM*B1Y+VP*B2Y)/BY*CS
      CS=CS-TEMP
      TEMPP=TEMP*VP
      TEMPM=TEMP*VM
      SPPY=-TEMPP
      SUPY=TEMPP*U(I,LYP)
      AS(I,J-1)=AS(I,J-1)-TEMPP
      AN(I,J)=TEMPM
      SP(I,J-1)=SP(I,J-1)+TEMPM
      SU(I,J-1)=SU(I,J-1)-TEMPM*U(I,LYP)
  400 CONTINUE
C********** FALSE SOURCES
	CP=AMAX1(0.0,(SMPW(J)+CW))
	SMPW(J)=-CW-CS
	SMPW(J-1)=SMPW(J-1)+CS
C********** ASSEMBLE COEFFICIENTS FOR E,W,N & S
      AW(I,J)=COEW+CW*UP
      AE(I-1,J)=AW(I,J)-CW+AE(I-1,J)
      AS(I,J)=COES+CS*VP
      AN(I,J-1)=AS(I,J)-CS+AN(I,J-1)
C********** CALCULATE SOURCES
      DUDXE=(U(I+1,J)-U(I,J))/SXCV(I+1)
      DUDXW=(U(I,J)-U(I-1,J))/SXCV(I)
      IF(J.NE.NJ)DVDXN=RV(J)*(V(I+1,J)-V(I,J))/UXCV(I)
      DVDXS=RV(J-1)*(V(I+1,J-1)-V(I,J-1))/UXCV(I)
      IF(J.NE.NJ)
     +GAMN=0.5*(VIS(I,J)+VIS(I+1,J)+FY(J)*(VIS(I,J+1)+VIS(I+1,J+1)
     +    -VIS(I,J)-VIS(I+1,J)))
      SU(I,J)=(DUDXE*VIS(I+1,J)-DUDXW*VIS(I,J))/UXCV(I)
     ++(DVDXN*GAMN-DVDXS*GAMS)/RSYCV(J)
     ++(P(I,J)-P(I+1,J))/UXCV(I)
      SU(I,J)=SU(I,J)*VOL+SUPX+SUPY
      SP(I,J)=SPPX+SPPY
      SU(I-1,J)=SU(I-1,J)+CP*U(I-1,J)
      SP(I-1,J)=SP(I-1,J)-CP
  100 CONTINUE
C********** PROBLEM MODIFICATION
      CALL MODU
C********** FINAL COEFF. ASSEMBLY AND RESIDUAL CALCULATION
      RESORU=0.0
	FAC=1.-URFU
      DO 200 I=2,NIM2
      DO 200 J=2,NJM1
      AP(I,J)=AW(I,J)+AE(I,J)+AS(I,J)+AN(I,J)-SP(I,J)
      DU(I,J)=RSYCV(J)/AP(I,J)
      RESOR=AN(I,J)*U(I,J+1)+AS(I,J)*U(I,J-1)+AE(I,J)*U(I+1,J)
     ++AW(I,J)*U(I-1,J)-AP(I,J)*U(I,J)+SU(I,J)
      IF(-SP(I,J).EQ.GREAT)RESOR=0.0
      RESORU=RESORU+ABS(RESOR)
C********** UNDER-RELAXATION
	AP(I,J)=AP(I,J)/URFU
	SU(I,J)=SU(I,J)+FAC*AP(I,J)*U(I,J)
	DU(I,J)=DU(I,J)*URFU
  200 CONTINUE
C********** SOLUTION OF DIFFERENCE EQUATIONS
      CALL LISOLV(2,2,NIM2,NJM1,U,NSWPU)
      RETURN
      END
C*************************     V-MOMENTUM       ********************
      SUBROUTINE CALCV

      INCLUDE 'UCOMMON.INC'
      LOGICAL LX,LY 
C********** ASSEMBLY OF COEFFICIENTS
      DO 100 I=2,NI
      DO 100 J=2,NJM1
C********** AREAS,VOLUME,DELX & DELY
      AREAS=SXCV(I)*R(J)
      AREAW=RVYCV(J)
      DELX1=UCVI(I-1)
      DELX2=UCVIP(I-1)
      DELY1=VCVJP(J-1)
      DELY2=VCVJ(J)
      B3X=B3XS(I-1)
      B3Y=B3YV(J)
      LX=I.GT.2.AND.I.LT.NI
      LY=J.GT.2.AND.J.LT.NJM1
      VOL=RVYCV(J)*SXCV(I)
C********** CALCULATE CONVECTION
      DENWN=DEN(I-1,J+1)+FX(I-1)*(DEN(I,J+1)-DEN(I-1,J+1))
      DENWS=DEN(I-1,J)+FX(I-1)*(DEN(I,J)-DEN(I-1,J))
      CW=0.5*(DENWN*U(I-1,J+1)*R(J+1)+DENWS*U(I-1,J)*R(J))*VYCV(J)
      DENSN=DEN(I,J)+FY(J)*(DEN(I,J+1)-DEN(I,J))
      DENSS=DEN(I,J-1)+FY(J-1)*(DEN(I,J)-DEN(I,J-1))
      CS=0.5*(DENSN*V(I,J)*RV(J)+DENSS*V(I,J-1)*RV(J-1))*SXCV(I)
C********** CALCULATE DIFFUSION
      GAMWN=VIS(I-1,J+1)+FX(I-1)*(VIS(I,J+1)-VIS(I-1,J+1))
      GAMWS=VIS(I-1,J)+FX(I-1)*(VIS(I,J)-VIS(I-1,J))
      GAMW=0.5*(GAMWN+GAMWS)
      DW=GAMW*AREAW/UXCV(I-1)
      DS=VIS(I,J)*AREAS/SYCV(J)
C********** SCHEMES
      NSUP=-1
      IF(CW.GT.0.0)NSUP=1
      UP=0.5*FLOAT(1+NSUP)
      UM=0.5*FLOAT(1-NSUP)
      NSVP=-1
      IF(CS.GT.0.0)NSVP=1
      VP=0.5*FLOAT(1+NSVP)
      VM=0.5*FLOAT(1-NSVP)
      IF(LX.AND.NQUICK.EQ.1)GO TO 111
      TEMPW=DW-ABS(CW)*0.1
      COEW=0.0D0
      TEMPW=TEMPW/(DW+TINY)
      IF(TEMPW.GT.TINY)COEW=DW*TEMPW**5
      IF(.NOT.LX)COEW=DW-AMAX1(FX(I-1)*CW,-(1.-FX(I-1))*CW)
      SPPX=0.0
      SUPX=0.0
      AE(I,J)=0.0
      GO TO 300
  111 KXP=I-1-NSUP
      LXP=KXP+(1-NSUP)/2
      DELX3=UM*DELX2-UP*DELX1-UXCV(KXP)*NSUP
      B1X=DELX2*DELX3*(DELX2-DELX3)
      B2X=DELX1*DELX3*(DELX1+DELX3)
      BX=B1X-B2X+B3X
      TEMP=B3X/BX*CW
      COEW=DW+(UM*B1X+UP*B2X)/BX*CW
      CW=CW-TEMP
      TEMPP=TEMP*UP
      TEMPM=TEMP*UM
      SPPX=-TEMPP
      SUPX=TEMPP*V(LXP,J)
      AW(I-1,J)=AW(I-1,J)-TEMPP
      AE(I,J)=TEMPM
      SP(I-1,J)=SP(I-1,J)+TEMPM
      SU(I-1,J)=SU(I-1,J)-TEMPM*V(LXP,J)
  300 CONTINUE
      IF(LY.AND.NQUICK.EQ.1)GO TO 222
      TEMPS=DS-ABS(CS)*0.1
      COES=0.0
      TEMPS=TEMPS/(DS+TINY)
      IF(TEMPS.GT.TINY)COES=DS*TEMPS**5
      IF(.NOT.LY.AND.NQUICK.EQ.1)COES=DS-0.5*ABS(CS)
      SPPY=0.0
      SUPY=0.0
      AN(I,J)=0.0
      GO TO 400
  222 KYP=J-NSVP
      LYP=KYP-(1+NSVP)/2
      DELY3=VM*DELY2-VP*DELY1-SYCV(KYP)*NSVP
      B1Y=DELY2*DELY3*(DELY2-DELY3)
      B2Y=DELY1*DELY3*(DELY1+DELY3)
      BY=B1Y-B2Y+B3Y
      TEMP=B3Y/BY*CS
      COES=DS+(VM*B1Y+VP*B2Y)/BY*CS
      CS=CS-TEMP
      TEMPP=TEMP*VP
      TEMPM=TEMP*VM
      SPPY=-TEMPP
      SUPY=TEMPP*V(I,LYP)
      AS(I,J-1)=AS(I,J-1)-TEMPP
      AN(I,J)=TEMPM
      SP(I,J-1)=SP(I,J-1)+TEMPM
      SU(I,J-1)=SU(I,J-1)-TEMPM*V(I,LYP)
  400 CONTINUE
C********** FALSE SOURCES
	CP=AMAX1(0.0,(SMPW(J)+CW))
	SMPW(J)=-CW-CS
	SMPW(J-1)=SMPW(J-1)+CS
C********** ASSEMBLE COEFFICIENTS FOR E,W,N & S
      AW(I,J)=COEW+CW*UP
      AE(I-1,J)=AW(I,J)-CW+AE(I-1,J)
      AS(I,J)=COES+CS*VP
      AN(I,J-1)=AS(I,J)-CS+AN(I,J-1)
C********** CALCULATE SOURCES
      DUDYE=(U(I,J+1)-U(I,J))/VYCV(J)
      DUDYW=(U(I-1,J+1)-U(I-1,J))/VYCV(J)
      DVDYN=R(J+1)*(V(I,J+1)-V(I,J))/SYCV(J+1)
      DVDYS=R(J)*(V(I,J)-V(I,J-1))/SYCV(J)
      IF(I.NE.NI)
     +GAME=0.5*(VIS(I,J+1)+VIS(I,J)+FX(I)*(VIS(I+1,J+1)+VIS(I+1,J)
     +    -VIS(I,J+1)-VIS(I,J)))
      SU(I,J)=(GAME*DUDYE-GAMW*DUDYW)/SXCV(I)
     ++(DVDYN*VIS(I,J+1)-DVDYS*VIS(I,J))/RVYCV(J)
     ++(P(I,J)-P(I,J+1))/VYCV(J)
	SP(I,J)=0.0
      IF(IND.EQ.2)SP(I,J)=-(VIS(I,J)+VIS(I,J+1))/((R(J)+R(J+1))*0.5)**2
      SU(I,J)=SU(I,J)*VOL+SUPX+SUPY
      SP(I,J)=SP(I,J)*VOL+SPPX+SPPY
      SU(I-1,J)=SU(I-1,J)+CP*V(I-1,J)
      SP(I-1,J)=SP(I-1,J)-CP
  100 CONTINUE
C********** PROBLEM MODIFICATION
      CALL MODV
C********** FINAL COEFF. ASSEMBLY AND RESIDUAL CALCULATION
      RESORV=0.0
	FAC=1.-URFV
      DO 200 I=2,NIM1
      DO 200 J=2,NJM2
      AP(I,J)=AW(I,J)+AE(I,J)+AS(I,J)+AN(I,J)-SP(I,J)
      DV(I,J)=0.5*(R(J)+R(J+1))*SXCV(I)/AP(I,J)
      RESOR=AN(I,J)*V(I,J+1)+AS(I,J)*V(I,J-1)+AE(I,J)*V(I+1,J)
     ++AW(I,J)*V(I-1,J)-AP(I,J)*V(I,J)+SU(I,J)
      IF(-SP(I,J).EQ.GREAT)RESOR=0.0D0
      RESORV=RESORV+ABS(RESOR)
C********** UNDER-RELAXATION
	AP(I,J)=AP(I,J)/URFV
	SU(I,J)=SU(I,J)+FAC*AP(I,J)*V(I,J)
	DV(I,J)=DV(I,J)*URFV
  200 CONTINUE
C********* SOLUTION OF DIFFERENCE EQUATIONS
      CALL LISOLV(2,2,NIM1,NJM2,V,NSWPV)
      RETURN
      END
C*************************   PRESSURE FIELD     ********************
      SUBROUTINE CALCP

      INCLUDE 'UCOMMON.INC'

      DO 100 I=2,NI
      DO 100 J=2,NJ
C********** AREAS
      AREAW=RSYCV(J)
      AREAS=SXCV(I)*RV(J-1)
C********** CALCULATE COEFFICIENTS
      DENW=DEN(I-1,J)+FX(I-1)*(DEN(I,J)-DEN(I-1,J))
      DENS=DEN(I,J-1)+FY(J-1)*(DEN(I,J)-DEN(I,J-1))
      AW(I,J)=DENW*AREAW*DU(I-1,J)
      AE(I-1,J)=AW(I,J)
      AS(I,J)=DENS*AREAS*DV(I,J-1)
      AN(I,J-1)=AS(I,J)
C********** CALCULATE SOURCES
      CW=DENW*U(I-1,J)*AREAW
      CS=DENS*V(I,J-1)*AREAS
      SU(I,J)=CW+CS
      IF(J.NE.2)SU(I,J-1)=SU(I,J-1)-CS
      IF(I.NE.2)SU(I-1,J)=SU(I-1,J)-CW
      SP(I,J)=0.0
  100 CONTINUE
C********** PROBLEM MODIFICATION
      CALL MODP
C********** FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION
      RESORM=0.0
      DO 200 I=2,NIM1
      DO 200 J=2,NJM1
      AP(I,J)=AW(I,J)+AE(I,J)+AS(I,J)+AN(I,J)-SP(I,J)
  200 RESORM=RESORM+ABS(SU(I,J))
C********** SOLUTION OF DIFFERENCE EQUATION
      CALL LISOLV(2,2,NIM1,NJM1,PP,NSWPP)
C********** CORRECT VELOCITY & PRESSURE
C**********VELOCITIES
      DO 400 I=2,NIM1
      DO 400 J=2,NJM1
      IF(I.NE.NIM1) U(I,J)=U(I,J)+DU(I,J)*(PP(I,J)-PP(I+1,J))
      IF(J.NE.NJM1) V(I,J)=V(I,J)+DV(I,J)*(PP(I,J)-PP(I,J+1))
  400 CONTINUE
C********** PRESSURE
      PPREF=PP(IPREF,JPREF)
      DO 500 I=2,NI
      DO 500 J=2,NJ
      P(I,J)=P(I,J)+URFP*(PP(I,J)-PPREF)
      PP(I,J)=0.0D0
  500 CONTINUE
C********** ENTRAINMENT ASSOCIATED WITH CONSTANT PRESSURE B C
C	CALL ENTRA
      RETURN
      END
C************************* TURB. KINETIC ENERG  ********************
      SUBROUTINE CALCTE

      INCLUDE 'UCOMMON.INC'
      LOGICAL LX,LY 
      
	NQUICK=NQKE
C********** ASSEMBLY OF COEFFICIENTS
      DO 100 I=2,NI
      DO 100 J=2,NJ
C********** AREAS,VOLUME,DELX,DELY & TVIS
      AREAW=RSYCV(J)
      AREAS=SXCV(I)*RV(J-1)
      VOL=RSYCV(J)*SXCV(I)
      DELX1=UCVI(I-1)
      DELX2=UCVIP(I-1)
      DELY1=VCVJ(J-1)
      DELY2=VCVJP(J-1)
      B3X=B3XS(I-1)
      B3Y=B3YS(J-1)
      LX=I.GT.2.AND.I.LT.NI
      LY=J.GT.2.AND.J.LT.NJ
	TVIS=VIS(I,J)-VISCOS+TINY
C********** CALCULATE CONVECTION
      DENW=DEN(I-1,J)+FX(I-1)*(DEN(I,J)-DEN(I-1,J))
      DENS=DEN(I,J-1)+FY(J-1)*(DEN(I,J)-DEN(I,J-1))
      CW=DENW*U(I-1,J)*AREAW
      CS=DENS*V(I,J-1)*AREAS
C********** CALCULATE DIFFUSION
      GAMW=VIS(I-1,J)+FX(I-1)*(VIS(I,J)-VIS(I-1,J))
      GAMS=VIS(I,J-1)+FY(J-1)*(VIS(I,J)-VIS(I,J-1))
      DW=GAMW*AREAW/UXCV(I-1)/PRTE
      DS=GAMS*AREAS/VYCV(J-1)/PRTE
C********** SCHEMES
      NSUP=-1
      IF(CW.GT.0.0)NSUP=1
      UP=0.5*FLOAT(1+NSUP)
      UM=0.5*FLOAT(1-NSUP)
      NSVP=-1
      IF(CS.GT.0.0)NSVP=1
      VP=0.5*FLOAT(1+NSVP)
      VM=0.5*FLOAT(1-NSVP)
      IF(LX.AND.NQUICK.EQ.1)GO TO 111
      TEMPW=DW-ABS(CW)*0.1
      COEW=0.00
      TEMPW=TEMPW/(DW+TINY)
      IF(TEMPW.GT.TINY)COEW=DW*TEMPW**5
      IF(.NOT.LX)COEW=DW-AMAX1(FX(I-1)*CW,-(1.-FX(I-1))*CW)
      SPPX=0.0
      SUPX=0.0
      AE(I,J)=0.0
      GO TO 300
  111 KXP=I-1-NSUP
      LXP=KXP+(1-NSUP)/2
      DELX3=UM*DELX2-UP*DELX1-UXCV(KXP)*NSUP
      B1X=DELX2*DELX3*(DELX2-DELX3)
      B2X=DELX1*DELX3*(DELX1+DELX3)
      BX=B1X-B2X+B3X
      TEMP=B3X/BX*CW
      COEW=DW+(UM*B1X+UP*B2X)/BX*CW
      CW=CW-TEMP
      TEMPP=TEMP*UP
      TEMPM=TEMP*UM
      SPPX=-TEMPP
      SUPX=TEMPP*TE(LXP,J)
      AW(I-1,J)=AW(I-1,J)-TEMPP
      AE(I,J)=TEMPM
      SP(I-1,J)=SP(I-1,J)+TEMPM
      SU(I-1,J)=SU(I-1,J)-TEMPM*TE(LXP,J)
  300 CONTINUE
      IF(LY.AND.NQUICK.EQ.1)GO TO 222
      TEMPS=DS-ABS(CS)*0.1
      COES=0.0
      TEMPS=TEMPS/(DS+TINY)
      IF(TEMPS.GT.TINY)COES=DS*TEMPS**5
      IF(.NOT.LY)COES=DS-AMAX1(FY(J-1)*CS,-(1.-FY(J-1))*CS)
      SPPY=0.0
      SUPY=0.0
      AN(I,J)=0.0
      GO TO 400
  222 KYP=J-1-NSVP
      LYP=KYP+(1-NSVP)/2
      DELY3=VM*DELY2-VP*DELY1-VYCV(KYP)*NSVP
      B1Y=DELY2*DELY3*(DELY2-DELY3)
      B2Y=DELY1*DELY3*(DELY1+DELY3)
      BY=B1Y-B2Y+B3Y
      TEMP=B3Y/BY*CS
      COES=DS+(VM*B1Y+VP*B2Y)/BY*CS
      CS=CS-TEMP
      TEMPP=TEMP*VP
      TEMPM=TEMP*VM
      SPPY=-TEMPP
      SUPY=TEMPP*TE(I,LYP)
      AS(I,J-1)=AS(I,J-1)-TEMPP
      AN(I,J)=TEMPM
      SP(I,J-1)=SP(I,J-1)+TEMPM
      SU(I,J-1)=SU(I,J-1)-TEMPM*TE(I,LYP)
  400 CONTINUE
C********** FALSE SOURCES
	CP=AMAX1(0.0,(SMPW(J)+CW))
	SMPW(J)=-CW-CS
	SMPW(J-1)=SMPW(J-1)+CS
C********** ASSEMBLE COEFFICIENTS FOR E,W,N & S
      AW(I,J)=COEW+CW*UP
      AE(I-1,J)=AW(I,J)-CW+AE(I-1,J)
      AS(I,J)=COES+CS*VP
      AN(I,J-1)=AS(I,J)-CS+AN(I,J-1)
C********** CALCULATE SOURCES
      DUDX=(U(I,J)-U(I-1,J))/SXCV(I)
      DVDY=(V(I,J)-V(I,J-1))/SYCV(J)
      IF(J.NE.NJ)
     +UN=0.5*(U(I,J)+U(I-1,J)+FY(J)*(U(I,J+1)+U(I-1,J+1)-U(I,J)-U(I-1,J)
     +))
      US=0.5*(U(I,J-1)+U(I-1,J-1)+FY(J-1)*(U(I,J)+U(I-1,J)-U(I,J-1)
     +  -U(I-1,J-1)))
      DUDY=(UN-US)/SYCV(J)
      IF(I.NE.NI)
     +VE=0.5*(V(I,J)+V(I,J-1)+FX(I)*(V(I+1,J)+V(I+1,J-1)-V(I,J)-V(I,J-1)
     +))
      VW=0.5*(V(I-1,J)+V(I-1,J-1)+FX(I-1)*(V(I,J)+V(I,J-1)-V(I-1,J)
     +  -V(I-1,J-1)))
      DVDX=(VE-VW)/SXCV(I)
	GENEW=2.*(DUDX**2+DVDY**2)+(DUDY+DVDX)**2
      IF(IND.EQ.2)GENEW=GENEW+2.*(0.5*(V(I,J)+V(I,J-1))/R(J))**2
	GEN(I,J)=GEN(I,J)+URFGEN*(TVIS*GENEW-GEN(I,J))
      SU(I,J)=GEN(I,J)
      SP(I,J)=-CMU*DEN(I,J)**2*ABS(TE(I,J))/TVIS
      SU(I,J)=SU(I,J)*VOL+SUPX+SUPY
      SP(I,J)=SP(I,J)*VOL+SPPX+SPPY
      SU(I-1,J)=SU(I-1,J)+CP*TE(I-1,J)
	SP(I-1,J)=SP(I-1,J)-CP
  100 CONTINUE
C********** PROBLEM MODIFICATION
      CALL MODTE
C***** FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION
      RESORK=0.0
	FAC=1.-URFK
      DO 200 I=2,NIM1
      DO 200 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      RESOR=AN(I,J)*TE(I,J+1)+AS(I,J)*TE(I,J-1)+AE(I,J)*TE(I+1,J)
     +     +AW(I,J)*TE(I-1,J)-AP(I,J)*TE(I,J)+SU(I,J)
      IF(-SP(I,J).EQ.GREAT)RESOR=0.0
      RESORK=RESORK+ABS(RESOR)
C********** UNDER-RELAXATION
	AP(I,J)=AP(I,J)/URFK
      SU(I,J)=SU(I,J)+FAC*AP(I,J)*TE(I,J)
  200 CONTINUE
C********* SOLUTION OF DIFFERENCE EQUATIONS
      CALL LISOLV(2,2,NIM1,NJM1,TE,NSWPK)
      RETURN
      END
C*************************   ENERGY DISSIPATION   ********************
      SUBROUTINE CALCED

      INCLUDE 'UCOMMON.INC'
      LOGICAL LX,LY 
C********** ASSEMBLY OF COEFFICIENTS
      DO 100 I=2,NI
      DO 100 J=2,NJ
C********** AREAS,VOLUME,DELX & DELY
      AREAW=RSYCV(J)
      AREAS=SXCV(I)*RV(J-1)
      VOL=RSYCV(J)*SXCV(I)
      DELX1=UCVI(I-1)
      DELX2=UCVIP(I-1)
      DELY1=VCVJ(J-1)
      DELY2=VCVJP(J-1)
      B3X=B3XS(I-1)
      B3Y=B3YS(J-1)
      LX=I.GT.2.AND.I.LT.NI
      LY=J.GT.2.AND.J.LT.NJ
	TVIS=VIS(I,J)-VISCOS+TINY
C********** CALCULATE CONVECTION
      DENW=DEN(I-1,J)+FX(I-1)*(DEN(I,J)-DEN(I-1,J))
      DENS=DEN(I,J-1)+FY(J-1)*(DEN(I,J)-DEN(I,J-1))
      CW=DENW*U(I-1,J)*AREAW
      CS=DENS*V(I,J-1)*AREAS
C********** CALCULATE DIFFUSION
      GAMW=VIS(I-1,J)+FX(I-1)*(VIS(I,J)-VIS(I-1,J))
      GAMS=VIS(I,J-1)+FY(J-1)*(VIS(I,J)-VIS(I,J-1))
      DW=GAMW*AREAW/UXCV(I-1)/PRED
      DS=GAMS*AREAS/VYCV(J-1)/PRED
C********** SCHEMES
      NSUP=-1
      IF(CW.GT.0.0)NSUP=1
      UP=0.5*FLOAT(1+NSUP)
      UM=0.5*FLOAT(1-NSUP)
      NSVP=-1
      IF(CS.GT.0.0)NSVP=1
      VP=0.5*FLOAT(1+NSVP)
      VM=0.5*FLOAT(1-NSVP)
      IF(LX.AND.NQUICK.EQ.1)GO TO 111
      TEMPW=DW-ABS(CW)*0.1
      COEW=0.0
      TEMPW=TEMPW/(DW+TINY)
      IF(TEMPW.GT.TINY)COEW=DW*TEMPW**5
      IF(.NOT.LX)COEW=DW-AMAX1(FX(I-1)*CW,-(1.-FX(I-1))*CW)
      SPPX=0.0D0
      SUPX=0.0D0
      AE(I,J)=0.0D0
      GO TO 300
  111 KXP=I-1-NSUP
      LXP=KXP+(1-NSUP)/2
      DELX3=UM*DELX2-UP*DELX1-UXCV(KXP)*NSUP
      B1X=DELX2*DELX3*(DELX2-DELX3)
      B2X=DELX1*DELX3*(DELX1+DELX3)
      BX=B1X-B2X+B3X
      TEMP=B3X/BX*CW
      COEW=DW+(UM*B1X+UP*B2X)/BX*CW
      CW=CW-TEMP
      TEMPP=TEMP*UP
      TEMPM=TEMP*UM
      SPPX=-TEMPP
      SUPX=TEMPP*ED(LXP,J)
      AW(I-1,J)=AW(I-1,J)-TEMPP
      AE(I,J)=TEMPM
      SP(I-1,J)=SP(I-1,J)+TEMPM
      SU(I-1,J)=SU(I-1,J)-TEMPM*ED(LXP,J)
  300 CONTINUE
      IF(LY.AND.NQUICK.EQ.1)GO TO 222
      TEMPS=DS-ABS(CS)*0.1
      COES=0.0
      TEMPS=TEMPS/(DS+TINY)
      IF(TEMPS.GT.TINY)COES=DS*TEMPS**5
      IF(.NOT.LY)COES=DS-MAX1(FY(J-1)*CS,-(1.-FY(J-1))*CS)
      SPPY=0.0D0
      SUPY=0.0D0
      AN(I,J)=0.0D0
      GO TO 400
  222 KYP=J-1-NSVP
      LYP=KYP+(1-NSVP)/2
      DELY3=VM*DELY2-VP*DELY1-VYCV(KYP)*NSVP
      B1Y=DELY2*DELY3*(DELY2-DELY3)
      B2Y=DELY1*DELY3*(DELY1+DELY3)
      BY=B1Y-B2Y+B3Y
      TEMP=B3Y/BY*CS
      COES=DS+(VM*B1Y+VP*B2Y)/BY*CS
      CS=CS-TEMP
      TEMPP=TEMP*VP
      TEMPM=TEMP*VM
      SPPY=-TEMPP
      SUPY=TEMPP*ED(I,LYP)
      AS(I,J-1)=AS(I,J-1)-TEMPP
      AN(I,J)=TEMPM
      SP(I,J-1)=SP(I,J-1)+TEMPM
      SU(I,J-1)=SU(I,J-1)-TEMPM*ED(I,LYP)
  400 CONTINUE
C********** FALSE SOURCES
	CP=AMAX1(0.0,(SMPW(J)+CW))
	SMPW(J)=-CW-CS
	SMPW(J-1)=SMPW(J-1)+CS
C********** ASSEMBLE COEFFICIENTS FOR E,W,N & S
      AW(I,J)=COEW+CW*UP
      AE(I-1,J)=AW(I,J)-CW+AE(I-1,J)
      AS(I,J)=COES+CS*VP
      AN(I,J-1)=AS(I,J)-CS+AN(I,J-1)
C********** CALCULATE SOURCES
      SU(I,J)=C1*CMU*DEN(I,J)*ABS(TE(I,J))*GEN(I,J)/TVIS
      SP(I,J)=-C2*CMU*DEN(I,J)**2*ABS(TE(I,J))/TVIS
      SU(I,J)=SU(I,J)*VOL+SUPX+SUPY
      SP(I,J)=SP(I,J)*VOL+SPPX+SPPY
      SU(I-1,J)=SU(I-1,J)+CP*ED(I-1,J)
      SP(I-1,J)=SP(I-1,J)-CP
  100 CONTINUE
C********** PROBLEM MODIFICATION
      CALL MODED
C***** FINAL COEFFICIENT ASSEMBLY AND RESIDUAL SOURCE CALCULATION
      RESORE=0.0
	FAC=1.-URFE
      DO 200 I=2,NIM1
      DO 200 J=2,NJM1
      AP(I,J)=AN(I,J)+AS(I,J)+AE(I,J)+AW(I,J)-SP(I,J)
      RESOR=AN(I,J)*ED(I,J+1)+AS(I,J)*ED(I,J-1)+AE(I,J)*ED(I+1,J)
     +     +AW(I,J)*ED(I-1,J)-AP(I,J)*ED(I,J)+SU(I,J)
      IF(-SP(I,J).EQ.GREAT)RESOR=0.0
      RESORE=RESORE+ABS(RESOR)
C********** UNDER-RELAXATION
      AP(I,J)=AP(I,J)/URFE
	SU(I,J)=SU(I,J)+FAC*AP(I,J)*ED(I,J)
  200 CONTINUE
C********* SOLUTION OF DIFFERENCE EQUATIONS
      CALL LISOLV(2,2,NIM1,NJM1,ED,NSWPE)
      RETURN
      END
C*************************   EQUATION SOLVER     ********************
      SUBROUTINE LISOLV(ISTART,JSTART,IEND,JEND,PHI,NSW)
      INCLUDE 'UCOMMON.INC'

      DIMENSION PHI(M,N),A(M),B(M),C(M),D(M)

      JSTM1=JSTART-1
      ISTM1=ISTART-1
      DO 2000 IT=1,NSW
      A(JSTM1)=0.0
C-----COMMENCE W-E SWEEP
      DO 100 I=ISTART,IEND
      C(JSTM1)=PHI(I,JSTM1)
C-----COMMENCE S-N TRAVERSE
      DO 101 J=JSTART,JEND
C-----ASSEMBLE TDMA COEFFICIENTS
      A(J)=AN(I,J)
      B(J)=AS(I,J)
      C(J)=AE(I,J)*PHI(I+1,J)+AW(I,J)*PHI(I-1,J)+SU(I,J)
      D(J)=AP(I,J)
C-----CALCULATE COEFFICIENTS OF RECURRENCE FORMULA
      TERM=1./(D(J)-B(J)*A(J-1))
      A(J)=A(J)*TERM
  101 C(J)=(C(J)+B(J)*C(J-1))*TERM
C-----OBTAIN NEW PHI@S
      DO 102 JJ=JSTART,JEND
      J=JEND+JSTART-JJ
  102 PHI(I,J)=A(J)*PHI(I,J+1)+C(J)
  100 CONTINUE
      A(ISTM1)=0.0
C-----COMMENCE S-N SWEEP
      DO 1000 J=JSTART,JEND
      C(ISTM1)=PHI(ISTM1,J)
C-----COMMENCE W-E TRAVERSE
      DO 1010 I=ISTART,IEND
C-----ASSEMBLE TDMA COEFFICIENTS
      A(I)=AE(I,J)
      B(I)=AW(I,J)
      C(I)=AN(I,J)*PHI(I,J+1)+AS(I,J)*PHI(I,J-1)+SU(I,J)
      D(I)=AP(I,J)
C-----CALCULATE COEFFICIENTS OF RECURRENCE FORMULA
      TERM=1./(D(I)-B(I)*A(I-1))
      A(I)=A(I)*TERM
 1010 C(I)=(C(I)+B(I)*C(I-1))*TERM
C-----OBTAIN NEW PHI@S
      DO 1020 II=ISTART,IEND
      I=IEND+ISTART-II
 1020 PHI(I,J)=A(I)*PHI(I+1,J)+C(I)
 1000 CONTINUE
 2000 CONTINUE
      RETURN
      END
C*************************      SUB. PRINT       ********************
	SUBROUTINE PRINT(ISTART,JSTART,NIEND,NJEND,XX,YY,PHI,HEAD)
      INCLUDE 'UCOMMON.INC'

	DIMENSION PHI(M,N),HEAD(6),STORE(M),XX(M),YY(N)

	ISKIP=1
	JSKIP=1
	WRITE(6,110)HEAD
	ISTA=ISTART-12
100	CONTINUE
	ISTA=ISTA+12
	IEND=ISTA+11
	IEND=MIN0(NIEND,IEND)
	WRITE(6,111)(I,I=ISTA,IEND,ISKIP)
	WRITE(6,112)
	DO 101 JJ=JSTART,NJEND,JSKIP
	J=JSTART+NJ-JJ
	DO 120 I=ISTA,IEND
	A=PHI(I,J)
	IF(ABS(A).LT.1.E-33) A=0.0
120	STORE(I)=A
101	WRITE(6,113) J,(STORE(I),I=ISTA,IEND,ISKIP),YY(J)
	WRITE(6,114) (XX(I),I=ISTA,IEND,ISKIP)
C-------------------------------------------------------------------
	IF(IEND.LT.NIEND) GO TO 100
	RETURN
110	FORMAT(1H0,20(2H*-),7X,6A6,7X,20(2H-*))
111	FORMAT(1H ,6H  I = ,I3,11I10,7X," Y = ")
112	FORMAT(3H  J)
113	FORMAT(1H ,I3,1P12E10.2,0PF7.3)
114	FORMAT(6H0X=   ,F7.3,11F10.3)
	END
C****************************END OF CODE*****************************
